/*
 * Copyright 2015 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

ext {
    versions = [:]
    libraries = [:]
}

versions.es = '1.6.0'
versions.guava = '18.0'
versions.jackson = '2.5.4'
versions.slf4j = '1.7.12'

libraries.guava = "com.google.guava:guava:$versions.guava"
libraries.hamcrest = "org.hamcrest:hamcrest-all:1.3"

configurations {
    plugin.description = 'The compile dependencies for the plugin, excluding the local dependencies to avoid problems with Shadow'
    compile.extendsFrom plugin
}

dependencies {
    plugin libraries.guava
    plugin 'org.projectlombok:lombok:1.16.4'
    plugin 'com.google.code.findbugs:jsr305:3.0.0'
    plugin 'joda-time:joda-time:2.8.1'

    plugin "com.fasterxml.jackson.core:jackson-core:$versions.jackson"
    plugin "com.fasterxml.jackson.core:jackson-databind:$versions.jackson"
    plugin "com.fasterxml.jackson.core:jackson-annotations:$versions.jackson"
    plugin "com.fasterxml.jackson.datatype:jackson-datatype-joda:$versions.jackson"

    plugin('net.logstash.logback:logstash-logback-encoder:4.3') {
        exclude group: 'ch.qos.logback', module: 'logback-core' // Already on Gradle classpath
    }

    plugin ("io.searchbox:jest:0.1.6") {
        exclude group: 'commons-logging'
    }
    plugin "org.elasticsearch:elasticsearch:$versions.es", optional

    plugin 'com.netflix.nebula:gradle-info-plugin:1.9.+', optional

    testCompile "com.netflix.nebula:nebula-test:$gradle.gradleVersion.+"
    testCompile "com.google.guava:guava-testlib:$versions.guava"
    testCompile 'com.github.tlrx:elasticsearch-test:1.2.1'
}

configurations.all ({
    resolutionStrategy {
        eachDependency { details ->
            switch (details.requested.name) {
                case 'guava':
                    details.useTarget libraries.guava
                    break
                case 'junit':
                case 'junit-dep':
                    details.useTarget 'junit:junit:4.11'
                    break
                case 'hamcrest-core':
                    // ElasticSearchIntegrationTest requires hamcrest-all, but it doesn't come in transitively
                    details.useTarget libraries.hamcrest
                    break
            }
        }
    }
})

// Drop the classifier and delete jar task actions to replace the regular jar artifact with the shadow artifact
shadowJar {
    configurations = [project.configurations.plugin]
    classifier = null
    dependencies {
        include(dependency(libraries.guava)) // Relocate Guava because it's a real bear if we get downgraded or upgraded downstream
    }
    relocate 'com.google.common', 'nebula.plugin.metrics.com.google.common'
    relocate 'com.google.thirdparty', 'nebula.plugin.metrics.com.google.thirdparty'
}

jar.deleteAllActions()
jar.dependsOn shadowJar

// Attempt to get out of Gradle dependency hell (at least somewhat reflect the runtime classloading scheme) by filtering out certain dependencies
// because it turns out that runtime classpath order isn't predictable

configurations {
    gradleApi
}

dependencies {
    gradleApi gradleApi()
    gradleApi localGroovy()
}

sourceSets.each {
    it.compileClasspath = it.compileClasspath - configurations.gradleApi + configurations.gradleApi
    it.runtimeClasspath = it.runtimeClasspath - configurations.gradleApi + configurations.gradleApi
}
